
// consts

version = "0.2"
usageMsg = "
<b>Dependencies</b>: metaxploit.so
<b>Usage</b>: 
	checklib libname.so     - check lib version in /lib/
	
	<b>Options</b>:
		-a, --all           - check all libs versions in /lib/
		-p, --path <path>   - check lib version in provided <path>
		-v, --version       - print version
        --help              - print this message

	<b>Usage examples</b>:
		checklib kernel_module.so
		checklib -a
		checklib -p my_folder/kernel_module.so
";



// functions

initMetaxploit = function() 
	metaxploitProgramPath = parent_path(program_path()) + "/metaxploit.so"
	metaxploitCurPath = get_abs_path("./metaxploit.so")
	metaxploitRootPath = "/lib/metaxploit.so"
	
	for path in [metaxploitProgramPath, metaxploitCurPath, metaxploitRootPath]
		lib = include_lib(path)
		if (typeof(lib) == "MetaxploitLib") then
			return lib; // MetaxploitLib found
		end if
	end for

	exit("\n<color=red>ERROR: metaxploit.so library NOT found!</color>\nPaths checked:\n   " + metaxploitProgramPath + "\n   " + metaxploitCurPath + "\n   " + metaxploitRootPath + "\n");
end function


getLib = function(path)
	metaxploit = initMetaxploit()
	lib = metaxploit.load(path)
	
	if (typeof(lib) == "MetaLib") then
		return lib
	else
		return null
	end if

end function


getAllLibs = function(path = "/lib")
	allLibsDir = get_shell().host_computer().File(path)
	allLibs = []
	
	// throw error
	if (allLibsDir == null or allLibsDir.is_folder == false) then
		exit("<color=red>no /lib folder on this machine</color>")
	end if
	
	files = allLibsDir.get_files()
	
	for file in files
		lib = getLib(file.path)
		
		if (lib != null) then
			allLibs.push(lib)
		end if
	
	end for

	// throw error
	if (allLibs.len == 0) then
		exit("<color=red>No libs found</color>")
	end if
	
	return allLibs
end function



// start script

libPath = null;
result = ""


// handle --help flag
if (params.len == 0 or params[0] == "--help") then
	exit(usageMsg)
end if


// handle --version flag
if (params[0] == "-v" or params[0] == "--version") then
	exit("\n<b>version: " + version + "</b>\n")
end if


// handle --path flag and init libPath
if (params[0] == "--path" or params[0] == "-p") then
	
	if (params.len < 2) then 
		print("\n<color=red>Path not provided</color>")
	else
		libPath = get_abs_path(params[1])
	end if

else if (params.len == 1) then
	libPath = "/lib/" + params[0]
end if


// handle -a flag
if params.len == 1 and (params[0] == "-a" or params[0] == "--all") then
	allLibs = getAllLibs()
	
	for lib in allLibs
			// result = result + "\n" + lib.lib_name + " " + lib.version 
			// escape characters don't work in UI text editor, but below works fine
		result = result + 
"
" 		+ lib.lib_name + " " + lib.version
	end for

	print("<b>" + result + "</b>")
	
	// save logs
	input = user_input("\n<b>Save results in checklib.log? [y/N]: </b>")
	if (input == "y" or input == "Y") then
		computer = get_shell().host_computer()
		file_created = computer.touch(current_path, "checklib.log")
		
		// print errors
		if (file_created == "The file already exists") then
			print("<i>checklib.log exists and will be overwritten</i>")
		else if (file_created != 1) then
			print(file_created)
		end if
	
		log_file = computer.File("checklib.log")
		log_file.set_content(result)
		print("<b>Log saved in " + log_file.path + "</b>")
	else
		print("<b>No log saved</b>")
	end if
	
else if (libPath != null) then
	lib = getLib(libPath)
	
	// throw error
	if (lib == null) then
		exit("\n<color=red>ERROR:</color>\n" + "<color=red>" + libPath + " is not a library</color>")
	end if

	result = "\n" + lib.lib_name + " " + lib.version
	
	print("<b>" + result + "</b>")
	print()
end if

